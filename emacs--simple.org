#+title: Simple emacs configuration
#+options: toc:nil
#+property: header-args :tangle ~/.emacs.d/init.el
#+startup: contents

#+begin_src elisp

  (require 'org)
  (require 'grep)
  (require 'bookmark)
  (require 'dired)

  (when (eq system-type 'gnu/linux)
    (custom-theme-set-faces
     'user
     '(variable-pitch ((t (:family "DejaVu Sans" :height 120 :weight normal))))
     '(fixed-pitch ((t ( :family "Source Code Pro" :height 110)))))

    (setq font-general "Source Code Pro 12")
    (set-frame-font font-general nil t)
    (add-to-list 'default-frame-alist `(font . ,font-general)))

  (when (eq system-type 'windows-nt)
    (setq home-dir "c:/users/jimbo")
    (let ((xPaths
           `(,(expand-file-name "~/bin")
             ,(expand-file-name "~/bin/PortableGit/bin")
             ,(expand-file-name "~/bin/PortableGit/usr/bin")
             ,(expand-file-name "~/bin/Apache-Subversion/bin/")
             ,(expand-file-name "~/bin/svn2git-2.4.0/bin")
             ,(expand-file-name "~/bin/clang/bin")
             ,(expand-file-name "~/bin/find")
             ,(expand-file-name "~/bin/omnisharp-win-x64")
             "c:/GnuWin32/bin"
             "c:/GNAT/2021/bin")))
      (setenv "PATH" (mapconcat 'identity xPaths ";"))
      (setq exec-path (append xPaths (list "." exec-directory))))

    (custom-theme-set-faces
     'user
     '(variable-pitch ((t (:family "Consolas" :height 110 :weight normal))))
     '(fixed-pitch ((t ( :family "Consolas" :height 110)))))

    (setq font-general "Consolas 11")
    (set-frame-font font-general nil t)
    (add-to-list 'default-frame-alist `(font . ,font-general)))

  ;; don't resize framee
  (setq frame-inhibit-implied-resize t)

  ;; inhibit startup screen
  (setq inhibit-startup-screen t)

  ;; title bar to print full path of visited filename
  (setq frame-title-format "%f")

  ;; turn on font-lock mode which shows colouring and formatting
  (global-font-lock-mode t)

  ;; enable(1) disable(-1) tool-bar / scroll-bar / menu-bar
  (tool-bar-mode -1)
  (scroll-bar-mode -1)
  (menu-bar-mode -1)

  ;; save the desktop state including frames / buffers and splits
  (desktop-save-mode -1)

  ;; show parenthesis matching
  (show-paren-mode t)

  ;; remember recent files opened
  (recentf-mode 1)
  (setq recentf-max-menu-items 200)
  (setq recentf-max-saved-items 200)

  ;; reverts externally changed files
  (global-auto-revert-mode t)

  ;; turn off audible bell
  (setq visible-bell t)
  (setq ring-bell-function 'ignore)

  ;; make ^K kill thru newline
  (setq kill-whole-line t)

  ;; set the completion styles
  (setq completion-styles '(basic partial-completion emacs22))

  ;; minibuffer completion for file and buffer switching
  (fido-mode 1)

  ;; minibuffer history to be saved
  (savehist-mode 1)

  ;; adds the column number in the modeline
  (column-number-mode 1)

  ;; revert windows with winner-undo
  (winner-mode 1)

  ;; always confirm before exiting emacs
  (setq confirm-kill-emacs 'y-or-n-p)

  (setq custom-safe-themes t)

  ;; always try and figure out which window to copy / move file
  (setq dired-dwim-target t)

  ;; org
  (setq org-startup-indented t)
  (setq org-use-speed-commands t)
  (setq org-hide-leading-stars t)

  ;; set up simple completion
  (setq-default abbrev-mode t)

  (setq hippie-expand-try-functions-list
        '(try-complete-file-name-partially try-complete-file-name
                                           try-expand-all-abbrevs try-expand-dabbrev
                                           try-expand-dabbrev-all-buffers try-expand-dabbrev-from-kill
                                           try-complete-lisp-symbol-partially try-complete-lisp-symbol))

  (custom-set-variables
   ;; custom-set-variables was added by Custom.
   ;; If you edit it by hand, you could mess it up, so be careful.
   ;; Your init file should contain only one such instance.
   ;; If there is more than one, they won't work right.
   '(custom-enabled-themes '(wombat))
   '(warning-suppress-log-types '((frameset)))
   '(warning-suppress-types '((frameset))))

  ;; keybindings
  (global-set-key (kbd "M-s =") #'ediff-buffers)
  (global-set-key (kbd "C-x x g") #'revert-buffer)
  (global-set-key (kbd "M-'") #'set-mark-command)
  (global-set-key (kbd "C-x [") #'beginning-of-buffer)
  (global-set-key (kbd "C-x ]") #'end-of-buffer)
  (global-set-key (kbd "C-x s") #'save-buffer)
  (global-set-key (kbd "M-m") #'(lambda ()(interactive)(scroll-down (/ (window-height) 4))))
  (global-set-key (kbd "M-n") #'(lambda ()(interactive)(scroll-up (/ (window-height) 4))))
  (global-set-key (kbd "C-o") #'other-window)
  (global-set-key (kbd "C-x C-b") 'ibuffer)
  (global-set-key (kbd "C-x j") 'winner-undo)
  (global-set-key (kbd "C-x k") 'winner-redo)
  (global-set-key (kbd "M-0") 'delete-window)
  (global-set-key (kbd "M-1") #'delete-other-windows)
  (global-set-key (kbd "M-2") #'split-window-vertically)
  (global-set-key (kbd "M-3") #'split-window-horizontally)
  (global-set-key (kbd "M-l") #'delete-other-windows)
  (global-set-key (kbd "M-j") #'split-window-vertically)
  (global-set-key (kbd "M-k") #'split-window-horizontally)
  (global-set-key (kbd "M-;") 'my/comment-or-uncomment)
  (global-set-key (kbd "M-[") #'yank)
  (global-set-key (kbd "M-]") #'yank-pop)
  (global-set-key (kbd "M-e") #'dired-jump)
  (global-set-key (kbd "M-u") #'tab-bar-switch-to-prev-tab)
  (global-set-key (kbd "M-i") #'tab-bar-switch-to-next-tab)
  (global-set-key (kbd "M-s j") #'eval-defun)
  (global-set-key (kbd "M-s h") #'my/mark-block)
  (global-set-key (kbd "M-s ,") #'my/mark-line)
  (global-set-key (kbd "M-s v") #'eval-expression)
  (global-set-key (kbd "M-9") #'hippie-expand)
  (global-set-key (kbd "M-s g") #'rgrep)
  (global-set-key (kbd "C-=") (lambda ()(interactive)(text-scale-adjust 1)))
  (global-set-key (kbd "C--") (lambda ()(interactive)(text-scale-adjust -1)))
  (global-set-key (kbd "C-x l") #'scroll-lock-mode)
  (global-set-key (kbd "M-g i") 'imenu)
  (global-set-key (kbd "C-c d") #'my/dired-duplicate-file)
  (global-set-key (kbd "C-c u") #'my/dired-du)
  (global-set-key (kbd "C-x ;") #'my/switch-to-thing)
  (global-set-key (kbd "C-c h") #'my/shell-create)
  (global-set-key (kbd "C-c m m") #'window-swap-states)
  (global-unset-key (kbd "C-h h"))
  (global-unset-key (kbd "C-t"))
  (global-unset-key (kbd "C-x m"))

  ;; window positioning
  (add-to-list 'display-buffer-alist
               '("\\*.*shell"
                 (display-buffer-reuse-window display-buffer-in-direction)
                 (direction . bottommost)
                 (dedicated . t)
                 (window-height . 0.2)
                 (inhibit-same-window . t)))

  ;; scrolling
  (setq scroll-margin 20)
  (setq scroll-preserve-screen-position t)

  ;; trash and backup in seperate location
  (setq delete-by-moving-to-trash t)
  (setq make-backup-files t)
  (setq backup-directory-alist '(("." . "~/backup"))
        backup-by-copying t ; Don't delink hardlinks
        version-control t ; Use version numbers on backups
        delete-old-versions t; automatically delete excess backups
        kept-new-versions 10 ; how many of the newest versions to keep
        kept-old-versions 5); how many of the old

  ;; delete selected text, may be more intuitive for newbie
  (setq delete-selection-mode nil)

  ;; allows all local variables for less friction
  (setq enable-local-variables :all)

  ;; no line wrapping
  (setq-default truncate-lines t)

  ;; indent using spaces
  (setq-default indent-tabs-mode nil)

  ;; tab indent
  (setq-default tab-width 4)

  ;; dired
  (setq dired-listing-switches "-alGgh")

  ;; I don't ever want a confirmation of a deletion
  (setq dired-auto-revert-buffer t)
  (setq dired-confirm-shell-command nil)
  (setq dired-no-confirm t)
  (setq dired-deletion-confirmer '(lambda (x) t))

  ;; always recursively delete
  (setq dired-recursive-deletes 'always)

  ;; grep
  (eval-after-load 'grep
    '(progn
       (dolist (dir '("nas" ".cache" "cache" "elpa" "chromium" ".local/share" "syncthing" ".mozilla" ".local/lib" "Games"))
         (push dir grep-find-ignored-directories))
       (dolist (file '(".cache" "*cache*" "*.iso" "*.xmp" "*.jpg" "*.mp4"))
         (push file grep-find-ignored-files))
       ))

  ;; jump map keybindings
  (defvar my-jump-keymap (make-sparse-keymap))
  (global-set-key (kbd "M-o") my-jump-keymap)

  (define-key my-jump-keymap (kbd "n") (lambda () (interactive) (find-file "~/nas")))
  (define-key my-jump-keymap (kbd "e") (lambda () (interactive) (find-file "~/.emacs.d/init.el")))
  (define-key my-jump-keymap (kbd "f") #'find-name-dired)
  (define-key my-jump-keymap (kbd "g") (lambda () (interactive) (find-file "~/.config")))
  (define-key my-jump-keymap (kbd "j") (lambda () (interactive) (find-file "~/DCIM/content/aaa--todo.org")))
  (define-key my-jump-keymap (kbd "l") #'recentf-open-files)
  (define-key my-jump-keymap (kbd "m") #'customize-themes)
  (define-key my-jump-keymap (kbd "o") #'bookmark-jump)
  (define-key my-jump-keymap (kbd "h") (lambda () (interactive) (find-file "~")))
  (define-key my-jump-keymap (kbd "k") (lambda () (interactive) (find-file (concat user-emacs-directory "emacs--simple.org"))))
  (define-key my-jump-keymap (kbd "r") (lambda () (interactive) (switch-to-buffer "*scratch*")))
  (define-key my-jump-keymap (kbd "w") (lambda () (interactive) (find-file "~/DCIM/content")))
  (define-key my-jump-keymap (kbd "-") #'tab-close)
  (define-key my-jump-keymap (kbd "=") #'tab-bar-new-tab)

  ;; visual changes map keybindings
  (defvar my-win-keymap (make-sparse-keymap))
  (global-set-key (kbd "C-q") my-win-keymap)

  (define-key my-win-keymap (kbd "b") #'(lambda () (interactive)(tab-bar-mode 'toggle)))
  (define-key my-win-keymap (kbd "c") #'display-fill-column-indicator-mode)
  (define-key my-win-keymap (kbd "d") #'window-divider-mode)
  (define-key my-win-keymap (kbd "e") #'whitespace-mode)
  (define-key my-win-keymap (kbd "f") #'font-lock-mode)
  (define-key my-win-keymap (kbd "h") #'global-hl-line-mode)
  (define-key my-win-keymap (kbd "l") #'my/sync-tab-bar-to-theme)
  (define-key my-win-keymap (kbd "m") #'my/load-theme)
  (define-key my-win-keymap (kbd "n") #'display-line-numbers-mode)
  (define-key my-win-keymap (kbd "p") #'variable-pitch-mode)
  (define-key my-win-keymap (kbd "q") #'toggle-menu-bar-mode-from-frame)
  (define-key my-win-keymap (kbd "s") #'my/toggle-internal-border-width)
  (define-key my-win-keymap (kbd "u") #'set-cursor-color)
  (define-key my-win-keymap (kbd "v") #'visual-line-mode)

  (setq tab-bar-new-tab-to 'rightmost)

  ;; ediff
  (setq ediff-window-setup-function 'ediff-setup-windows-plain)
  (setq ediff-highlight-all-diffs t)
  (setq ediff-split-window-function 'split-window-horizontally)

  ;; functions
  (defun darken-color (color percent)
    "Return a darker shade of COLOR by reducing its brightness by PERCENT."
    (let* ((rgb (color-values color))
           (factor (/ (- 100 percent) 100.0))
           (darker-rgb (mapcar (lambda (x) (max 0 (round (* x factor)))) rgb)))
      (apply 'format "#%02x%02x%02x" (mapcar (lambda (x) (/ x 256)) darker-rgb))))

  (defun set-hl-line-darker-background ()
    "Set the hl-line background to a slightly darker shade of the default background,
           preserving the original foreground colors of the current line."
    (interactive)
    (require 'hl-line)
    (unless global-hl-line-mode
      (global-hl-line-mode 1))
    (when (facep 'hl-line)
      (let* ((bg (face-background 'default))
             (darker-bg (darken-color bg 20)))
        (custom-set-faces
         `(hl-line ((t (:background ,darker-bg))))))))

  (defun my/load-theme ()
    "Prompt to select a theme from available themes and load the selected theme."
    (interactive)
    (let ((theme (completing-read "Choose theme: " (mapcar 'symbol-name (custom-available-themes)))))
      (dolist (item custom-enabled-themes)
        (disable-theme item))
      (load-theme (intern theme) t)))

  (defun my/switch-to-thing ()
    "Switch to a buffer, open a recent file, jump to a bookmark,
                                                                     or change the theme from a unified interface."
    (interactive)
    (let* ((buffers (mapcar #'buffer-name (buffer-list)))
           (recent-files recentf-list)
           (bookmarks (bookmark-all-names))
           (all-options (append buffers recent-files bookmarks))
           (selection (completing-read "Switch to: " all-options)))
      (pcase selection
        ((pred (lambda (sel) (member sel buffers))) (switch-to-buffer selection))
        ((pred (lambda (sel) (member sel bookmarks))) (bookmark-jump selection))
        (_ (find-file selection)))))

  (with-eval-after-load 'dired
    (define-key dired-mode-map (kbd "C-o") nil))

  (defun my/dired-du ()
    "Run 'du -hc' on the directory under the cursor in Dired."
    (interactive)
    (let ((current-dir (dired-get-file-for-visit)))
      (if (file-directory-p current-dir)
          (dired-do-async-shell-command "du -hc" nil (list current-dir))
        (message "The current point is not a directory."))))

  (defun my/sync-tab-bar-to-theme ()
    "Synchronize tab-bar faces with the current theme."
    (interactive)
    (set-hl-line-darker-background)
    (set-face-attribute 'mode-line nil :height 120 :underline nil :overline nil :box nil
                        :background "#ff8c00" :foreground "#000000")
    (set-face-attribute 'mode-line-inactive nil :height 120 :underline nil :overline nil
                        :background "#000000" :foreground "#aaaaaa")
    (let ((default-bg (face-background 'default))
          (default-fg (face-foreground 'default))
          (default-hl (face-background 'highlight))
          (inactive-fg (face-foreground 'mode-line-inactive)))
      (custom-set-faces
       '(vertical-border ((t (:foreground "#000000"))))
       '(window-divider ((t (:foreground "#000000"))))
       `(tab-bar ((t (:inherit default :background ,default-bg :foreground ,default-fg))))
       `(tab-bar-tab ((t (:inherit 'highlight :background "#ff8c00" :foreground "#000000"))))
       `(tab-bar-tab-inactive ((t (:inherit default :background ,default-bg :foreground ,inactive-fg)))))))

  (my/sync-tab-bar-to-theme)

  (defun my/shell-create (name)
    "Create a custom-named eshell buffer with NAME."
    (interactive "sName: ")
    (eshell 'new)
    (let ((new-buffer-name (concat "*eshell-" name "*")))
      (rename-buffer new-buffer-name t)))

  (defun my/dired-duplicate-file (arg)
    "Duplicate a file from DIRED with an incremented number.
                                                  If ARG is provided, it sets the counter."
    (interactive "p")
    (let* ((file (dired-get-file-for-visit))
           (dir (file-name-directory file))
           (name (file-name-nondirectory file))
           (base-name (file-name-sans-extension name))
           (extension (file-name-extension name t))
           (counter (if arg (prefix-numeric-value arg) 1))
           (new-file))
      (while (and (setq new-file
                        (format "%s%s_%03d%s" dir base-name counter extension))
                  (file-exists-p new-file))
        (setq counter (1+ counter)))
      (if (file-directory-p file)
          (copy-directory file new-file)
        (copy-file file new-file))
      (dired-revert)))

  (defun my/find-file ()
    "Find file from current directory in many different ways."
    (interactive)
    (let* ((find-options '(("find -type f -printf \"$PWD/%p\\0\"" . :string)
                           ("fd --absolute-path --type f -0" . :string)
                           ("rg --follow --files --null" . :string)
                           ("find-name-dired" . :command)))
           (selection (completing-read "Select : " find-options))
           (file-list)
           (file))
      (pcase (alist-get selection find-options nil nil #'string=)
        (:command
         (call-interactively (intern selection)))
        (:string
         (setq file-list (split-string (shell-command-to-string selection) "\0" t))
         (setq file (completing-read
                     (format "Find file in %s: "
                             (abbreviate-file-name default-directory))
                     file-list))))
      (when file (find-file (expand-file-name file)))))

  (defun my/mark-line ()
    "Mark whole line."
    (interactive)
    (beginning-of-line)
    (push-mark (point) nil t)
    (end-of-line))

  (defun my/mark-block ()
    "Marking a block of text surrounded by a newline."
    (interactive)
    (when (not (region-active-p))
      (backward-char))
    (skip-chars-forward " \n\t")
    (re-search-backward "^[ \t]*\n" nil 1)
    (skip-chars-forward " \n\t")
    (when (not (region-active-p))
      (push-mark))
    (re-search-forward "^[ \t]*\n" nil 1)
    (skip-chars-backward " \n\t")
    (setq mark-active t))
  (global-unset-key (kbd "C-z"))

  (defun my/comment-or-uncomment ()
    "Comments or uncomments the current line or region."
    (interactive)
    (if (region-active-p)
        (comment-or-uncomment-region
         (region-beginning)(region-end))
      (comment-or-uncomment-region
       (line-beginning-position)(line-end-position))))

  (setq window-divider-default-bottom-width 4)
  (setq window-divider-default-right-width 4)
  (setq window-divider-default-places t)
  (window-divider-mode -1)

  (set-fringe-mode '(0 . 0))

  (defvar my/internal-border-width 10 "Default internal border width for toggling.")

  (defun my/toggle-internal-border-width (&optional value)
    "Toggle internal border width given VALUE."
    (interactive "P")
    (let ((new-value (if value
                         value
                       (if (= (or (frame-parameter nil 'internal-border-width) 0)
                              0)
                           my/internal-border-width
                         0))))
      (modify-all-frames-parameters `((internal-border-width . ,new-value)))))

  ;; (modify-all-frames-parameters `((internal-border-width . ,my/internal-border-width)))

  (setq ispell-local-dictionary "en_GB")
  (setq ispell-program-name "hunspell")

#+end_src
